plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

apply from: '../config/jacoco/module.gradle'
apply from: '../config/detekt/detekt.gradle'

android {
    compileSdk 33

    defaultConfig {
        applicationId "com.example.takeaway"
        minSdk 27
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    signingConfigs {
        def keystoreProperties = new Properties()
        def keystorePropertiesFile = rootProject.file('keystore/key.properties')
        if (keystorePropertiesFile.exists()) {
            keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
            config {
                storeFile rootProject.file('keystore/android.keystore')
                storePassword keystoreProperties['storePass']
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPass']
            }
        } else {
            config {
                storeFile rootProject.file('keystore/android.keystore')
                storePassword System.getenv('storePass')
                keyAlias System.getenv('keyAlias')
                keyPassword System.getenv('keyPass')
            }
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.config
        }
        release {
            minifyEnabled true
            signingConfig signingConfigs.config
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        freeCompilerArgs += '-opt-in=kotlin.RequiresOptIn'
        freeCompilerArgs += '-opt-in=kotlin.Experimental'
        jvmTarget = '11'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {

    implementation project(':data')
    implementation project(':domain')

    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.8.0'
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material:material:$compose_material_version"
    implementation "androidx.compose.material:material-icons-extended:$compose_material_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation 'androidx.activity:activity-compose:1.6.1'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.0'
    implementation "androidx.navigation:navigation-compose:2.6.0-alpha07"
    implementation "androidx.hilt:hilt-navigation-compose:1.0.0"
    implementation "androidx.glance:glance-appwidget:1.0.0-alpha05"
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"

    implementation "com.google.accompanist:accompanist-pager:$accompanist_version"
    implementation "com.google.accompanist:accompanist-pager-indicators:$accompanist_version"
    implementation "com.google.accompanist:accompanist-flowlayout:$accompanist_version"

    implementation 'com.google.android.exoplayer:exoplayer-core:2.18.4'

    implementation 'com.jakewharton.timber:timber:5.0.1'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'io.mockk:mockk:1.13.3'
    testImplementation 'io.kotest:kotest-assertions-core:5.0.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
    testImplementation 'io.cucumber:cucumber-java8:7.11.1'
    testImplementation 'io.cucumber:cucumber-junit:7.11.1'
}
